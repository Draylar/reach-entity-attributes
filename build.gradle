plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id "maven-publish"
    id "signing"
}

group = maven_group
version = mod_version

final isReleaseVersion = !version.endsWith("-SNAPSHOT")

java {
    withSourcesJar()
}

loom {
    refmapName = "refmap.reach-entity-attributes.json"
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    modImplementation fabricApi.module("fabric-resource-loader-v0", fabric_version)
    implementation("com.google.code.findbugs:jsr305:$jsr305_version") {
        transitive = false
    }
}

compileJava {
    options.with {
        it.release.set(16)
        it.fork = true
        it.deprecation = true
        it.encoding = "UTF-8"
        it.compilerArgs = ["-Xlint:all", "-parameters"]
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("/fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    from "/LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            afterEvaluate {
                artifact remapJar
                artifact(sourcesJar.archiveFile) {
                    classifier = "sources"
                    builtBy remapSourcesJar
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://maven.jamieswhiteshirt.com/libs-" + (isReleaseVersion ? "release" : "snapshot")
            credentials {
                username = properties.mavenUser
                password = properties.mavenPassword
            }
        }
    }
}

signing {
    required = isReleaseVersion
    sign publishing.publications.mavenJava
}
